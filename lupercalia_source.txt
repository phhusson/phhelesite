"Lupercalia" by "Eric Forgeot"

The story genre is "Conte".   
The story creation year is 2008.
The story headline is "Un conte interactif".
The story description is "Le gardien du troupeau s[']était fâché contre le cerf qui n[']en faisait toujours qu[']à sa tête. Effectivement, rien ne pouvait le tempérer ou lui faire entendre raison. Le cerf était alors venu vous trouver, et vous avait interrogé sur ce que vous vouliez devenir dans cette nouvelle vie".

Release along with the source text, cover art, a website. [, the introductory booklet.]

Part 1 - Configuration du jeu

Chapter 1 - Configuration mémoire et paramètres spéciaux

Use MAX_VERBSPACE of 8000. 
Use MAX_PROP_TABLE_SIZE of 60000. 
Use MAX_ZCODE_SIZE of 60000. 

Use MAX_ARRAYS of 1500. 
Use MAX_CLASSES of 200. 
Use MAX_VERBS of 300. 
Use MAX_STATIC_DATA of 135000. 
Use MAX_INDIV_PROP_TABLE_SIZE of 20000. 
Use MAX_DICT_ENTRIES of 1500.

Use dynamic memory allocation of at least 16384.
Use maximum indexed text length of at least 2000.
Use memory economy.

Chapter 2 - Inclusion d[']extensions

Include French by Eric Forgeot. 
Use French Language.

Include Basic Screen Effects by Emily Short.

[Include Locksmith-fr by Eric Forgeot. original by Emily Short]
[Include Locksmith by Emily Short.]

Include Simple Chat fr by Eric Forgeot. [original by Mark Tilford]

Chapter 3 - Nouvelles définitions

Use no scoring.
Use full-length room descriptions.


A person can be informed. A person can be ready.

A person can be melancolique. A person is usually not melancolique.

A person can be awake. A person is usually awake.

A person can be living or dead. A person is usually living.

A person can be standingup or lying. A person is usually standingup.

A person can be humanbeing or animalbeing. The player is animalbeing. Compagnon is humanbeing.

A person can be seduced. A person is usually not seduced.
A person can be betrayed. A person is usually not betrayed.

An animal can be docile or aggressive. An animal is usually aggressive.

A thing can be full or empty. A thing is usually full.

Instead of taking something when the player is carrying something:
	if the player is animalbeing
		begin;
			say "Vous ne pouvez pas prendre plusieurs objets en même temps.";
		otherwise;
			continue the action;
		end if.

[Weapon is a kind of thing.]

A person can be masculin or feminin or neutre. A person is usually neutre.

Chapter 4 - Styles (for glulx only)

Before starting the virtual machine:
   initialize emphasis style;
   initialize statusline style.

To initialize emphasis style:
(-
glk_stylehint_set(wintype_TextBuffer,style_Emphasized,stylehint_Weight,0);
glk_stylehint_set(wintype_TextBuffer,style_Emphasized,stylehint_Oblique,1);
-)

To initialize statusline style:
(-
glk_stylehint_set(wintype_TextGrid,style_Normal,styleHint_BackColor,$000000 );

glk_stylehint_set(wintype_TextGrid,style_Normal, stylehint_TextColor,$ffffff );
-)

Chapter 5 - Médias

Figure of couverture is the file "couverture.png".
[Figure of blason ours is the file "blason_ours.png".
Figure of carte is the file "carte.png".
Sound of musique intro is the file "heures_du_vent_intro.ogg".
Sound of musique outro is the file "heures_du_vent_outro.ogg".
The File of nosound is called "nosound".]

Chapter 6 - Nouveaux verbes

Understand "suivre" and "suivre [someone]" as a mistake ("Vous ne suivez que votre instinct !")

Understand "suivre [something]" as a mistake ("Vous ne pouvez suivre cela.")

To say lire aide:
	say "Cette histoire ne fonctionne pas selon la logique habituelle des livres et des romans : ici c[']est au lecteur de prendre le héros en main et de décider de sa destinée. Il suffit donc de taper les instructions à passer au héros avec des phrases simples, et dans la mesure du possible l[']histoire se modifiera selon les directives du lecteur. Tout n[']est pas possible et une trame narrative existe malgré tout, mais des histoires diverses sont malgré tout envisageables en fonction des choix du lecteur. [line break]Pour avoir des indications supplémentaires, veuillez taper 'astuces', ou 'solution' pour la solution complète".

Understand "aide" as a mistake ("[lire aide]")

[Understand "crier" or "hurler" or "grogner" or "aboyer" as a mistake ("Un hurlement sinistre s[']élève de votre gorge.")]

Understand the command "shout" as something new.
 
Shouting is an action applying to nothing.
Understand "shout" or "crier" or "hurler" or "grogner" or "aboyer" as shouting.

Carry out shouting:
	say "Un hurlement sinistre s[']élève de votre gorge.";
	if the player is carrying something, say "Vous laissez tomber ce que vous avez avec vous.";
	now every thing carried by the player is in the location;
	

Hinting is an action applying to nothing.
Understand "astuces" or "astuce" or "hint" or "hints" as hinting.

Carry out hinting:
	say "Essayez tout d[']abord de savoir qui vous êtes. Profitez-en pour explorer les environs et peut-être trouver certaines reliques qui aideront à faire raconter des choses à l[']homme que vous rencontrez, qui à leur tour auront une action sur votre destiné.[paragraph break]Il n[']y a pas d[']autres astuces pour le moment.". [todo : améliorer la phrase]


Solutioning is an action applying to nothing.
Understand "solution" as solutioning.

Carry out solutioning:
	say "[paragraph break]
	Voici la solution :[line break]";
	say "sud/prendre laniere/nord/nord/laisser laniere/nord/ouest/regarder la mer/fouiller les roseaux/prendre le drapeau/est/sud/est/x homme/donner etendard a homme/ouest/ouest/est/nord/attendre/attendre/prendre la crosse[paragraph break]";
	say "première possibilité : sud/ouest/ouest/sud/prendre la laniere/nord/est/examiner la creature/attendre/attendre/attendre/attendre/se lever/prendre le vetement/mettre le vetement/ouest/sud/sud/sud/regarder les dessins/sud[paragraph break]";
	say "seconde possibilité : est/attaquer les loups avec la crosse/attaquer le vieux loup/ouest/sud/ouest/ouest/sud/prendre la laniere de cuir/nord/est/est[paragraph break]".

Understand "avancer" as north.
Understand "reculer" as south.

Attacking it with is an action applying to one thing and one carried thing.

Understand "attack [something] avec [something]" as attacking it with.

Understand "attack [someone] with [something]" as attacking it with.

Understand "attaquer [something] with [something]" as attacking it with.

Understand "attaquer [someone] avec [something]" as attacking it with.

Understand "frapper [something] with [something]" as attacking it with.

Understand "frapper [something] avec [something]" as attacking it with.

Understand "frapper a [something]" as attacking.

Understand "frapper [something]" as attacking.
Understand "casser [something]" as attacking.
Understand "attaquer [something]" as attacking.

[todo : remplacer frapper 'a' dans French]

Understand "frapper [someone] avec [something]" as attacking it with.

Understand "taper [something] with [something]" as attacking it with.

Understand "taper [someone] avec [something]" as attacking it with.


Instead of attacking someone with something, try attacking the noun.

Talking to is an action applying to one thing. 
Carry out talking to:
	say "Vous n[']avez rien à dire.".
	
Understand "talk to [someone]" and "greet [someone]" and "speak to [someone]" and "parler avec [someone]"and "parler avec [something]" and "parler a [someone]" and "parler a [something]" and "parler à [something]" and "parler au [someone]" and "parler aux [someone]" and "interroger [someone]" as talking to.


Filling is an action applying to one thing.
Understand "remplir [something]" as filling.
Understand "emplir [something]" as filling.

Filling it with is an action applying to two things.
Understand "remplir [something] avec [something]" as filling it with.
Understand "emplir [something] avec [something]" as filling it with.

Pouring it into is an action applying to two things. [ and one carried thing.]
Understand "verser [something] dans [something]" as pouring it into.
Understand "verser [something] sur [something]" as pouring it into.

Carry out pouring it into:
	say "Cela n[']était pas une action qu[']il pouvait normalement effectuer à ce moment.".

Instead of jumping, say "Vous faites quelques bonds sur place.".

Touching it with is an action applying to two things.

Understand "touch [something] with [something]" as touching it with.

Rubbing it with is an action applying to two things.

Understand "rub [something] avec [something]" as rubbing it with.

Understand "utiliser [something] avec [something]" as tying it to.

Understand "melanger [something] avec [something]" as tying it to.


Understand "credits" as a mistake ("Histoire et code par Eric Forgeot. Image de couverture par Maryam Gousheh Forgeot.")


Chapter 6 - Routines diverses

The carrying capacity of the player is always 1.

Part 2 - Le jeu

Chapter 1 - Preparation

When play begins:
	clear the screen;
	say "[the story description] :";
	run a conversation from player choix;
	if the player is neutre
	begin;
		say "Le cerf va donc choisir pour vous.";
		if a random chance of 50 in 100 succeeds
			begin;
				now the player is masculin;
			otherwise;
				now the player is feminin;
		end if;
	end if;
	say "Ainsi soit-il !" ; wait for any key ; clear the screen;
		[todo : peut être retirer commentaire pour release]
	Display the figure of couverture;
	[if file of nosound exists
		begin;
			do nothing;
		otherwise;
			Play the Sound of musique intro;
	end if.]

[ -- choix du sexe-- ]


Player choix, player loup, player louve are chat nodes.

Instead of giving text for player choix: say "" ; Instead of finding responses to player choix: link to player loup; link to player louve.

Instead of giving link to player loup: say "Choisir de devenir son fils". Instead of giving text for player loup: say "" ; now  player is masculin; deactivate player choix.

Instead of giving link to player louve: say "Choisir de devenir sa fille". Instead of giving text for player louve: say "" ; now  player is feminin ; deactivate player choix.

[ -- ]

Instead of examining player, say "Vous ne pouvez pas vous voir ici, il faudrait une surface faisant de la réflexion pour cela.".

Instead of examining player when the location is mer, try examining ocean.

Chapter 2 - Les scènes

Part1 is a scene. Part1 begins when play begins. Part1 ends when nuit ends.

Crepuscule is a scene. Crepuscule begins when play begins.

Crepuscule ends when the time since Crepuscule began is 5 minutes.

Nuit is a scene. Nuit begins when crepuscule ends. Nuit ends when the meute de loups is in void.

When nuit begins:
	now the printed name of plaine1 is "La plaine sous la nuit";
	remove soleil from play.

Chasse is a scene. Chasse begins when the compagnon is in plaine5 and the player is in plaine5.
Chasse ends when the time since Chasse began is 15 minutes.

When chasse begins:
	say "Vos yeux rencontrent ceux [if player is feminin]du berger[otherwise]de la bergère[end if], et vous sentez comme une connivence avec [if player is feminin]celui-ci[otherwise]celle-ci[end if]. Vous avez l[']impression que ce sentiment est partagé de son côté, comme si vous l[']aviez déjà rencontré[if player is feminin][otherwise]e[end if] auparavant.".
	
Every turn during chasse:
	if the time since chasse began is 2 minutes
		begin;
		say "Une meute de loup s[']approche alors [if player is feminin]du berger[otherwise]de la bergère[end if] qui s[']enfuit aussitôt laissant tomber sa crosse, et une poursuite sans relâche s[']ensuit en direction de la forêt à l[']est.";
		move crosse to location;
		move compagnon to foret;
		remove moutons from play;
		move meute de loups to foret;
		end if.

When chasse ends:
	if meute de loups is in void
	begin;
		do nothing;
	otherwise;
		say "Un cri déchirant trouble la nuit et votre âme. Vous retournez vers la forêt, mais il est trop tard. Une lueur vient alors recouvrir le corps [if player is feminin]du berger[otherwise]de la bergère[end if], et un faune de grande taille, visiblement hors de lui, fait son apparition dans ce lieu.";
		End the game saying "Il vous accuse d[']avoir volé un de ses enfants, et vous bannit de cette terre pour ce qu[']il croit être votre crime.";
	end if.

Attaque is a scene. Attaque begins when chasse begins.
Attaque ends when the meute de loups is in void.

Every turn during attaque:
	if the time since chasse began is 6 minutes and marcheur is not in location, say "Les grondements des loups deviennent de plus en plus forts, et sa victime se met à hurler à son tour.".

Rencontre is a scene. Rencontre begins when Faune is in location.

When rencontre begins:
	remove flute from play;
	move Marcheur to location.

Rencontre ends when Faune is in void.

Every turn during rencontre:
	if the time since rencontre began is 2 minutes, say "[if player is in location]Le faune fait une petite moue en direction de l[']homme, et lui dit 'Alors, tu as enfin récupéré ta bête ? Essaye de ne pas les laisser effrayer mes protégés veux-tu ?'[end if]";
	if the time since rencontre began is 3 minutes
		begin;
		[now grottes2 is south from grottes;]
		say "Le faune prend une lanière de cuir des mains de l[']autre homme, et en frappe doucement [if player is masculin]la bergère[otherwise]le berger[end if], tout en éclatant d[']un rire sympathique et jovial. Le visage cramoisi, [if player is masculin]cette dernière[otherwise]ce dernier[end if] se relève alors, et s[']enfuit à travers les plaines, puis vers le sud.[line break]";
		move compagnon to grottes2;
		say "[if player is in location]Le faune donne ensuite un bon coup de pied au derrière du vieux loup qui n[']ose plus rien faire. Puis il dit à l[']homme : 'Merci quand même pour ton aide, vieux cerf.' Il se tourne alors vers vous, et vous regarde de ses inquiétants yeux jaunes. 'Toi au moins tu ne leur feras plus de mal !'. Vous tombez alors sur le sol, frappé par un souffle surnaturel, avec une forte angoisse qui vous prend au ventre. Lorsque vous reprenez vos esprits, l[']homme-bouc n[']est plus là, seul reste l[']autre, avec son loup.[end if]";
		move faune to void;
		now the player is humanbeing;
		now the carrying capacity of the player is 2;
		[silently try sitting down;]
		silently try entering sol;
		now the player is lying;
		end if.


Epilogue1 is a scene. Epilogue1 begins when the player is humanbeing.

Every turn during epilogue1:
	if the time since rencontre began is 4 minutes
		begin;
		say "";
		move marcheur to location; [au cas où]
		say "Il jette un vêtement à vos pieds et dit 'Tiens, prends cela humain, maintenant tu n[']auras plus le loisir de te promener nu comme notre ami Pan !'";
		move vetements to location;
		end if;
	if the location is grottes2
		begin;
		say "[if player is masculin]La jeune femme vous attend avec une torche à la main. Elle sourit de vous voir ainsi, et visiblement vous a reconnu. Elle vous regarde avec les yeux emplis de désir et vous tend sa main. De l[']autre elle pointe en direction d[']un passage qui semble s[']ouvrir au loin à l[']air libre.[otherwise]Le jeune homme vous attend avec une torche à la main, surpris de vous voir sous votre nouvelle apparence. Il vous tend la main et vous embrasse dans le cou, tout en vous indiquant au loin la fin du passage qui s[']ouvre à l[']air libre.[end if] Une nouvelle vie va pouvoir commencer pour vous deux.";
		end the game in victory;
		end if.

When epilogue1 begins:
	say "". 
	
Epilogue2 is a scene. Epilogue2 begins when the compagnon is animalbeing.

Every turn during epilogue2:
	move compagnon to location;
	say "Votre [if player is feminin]compagnon[otherwise]compagne[end if] vous accompagne.".

Chapter 3 - Les lieux 

Section 1 - Le domaine du joueur

Plaine1 is a room. The description of plaine1 is "Le vent souffle dans la plaine, les herbes ondulent sous ce vent. [if crepuscule is happening]Le soleil va bientôt se coucher, et projette ses rayons ainsi que les ombres qu[']il provoque très loin vers l[']horizon. Les bergers et les bergères tremblent pour leurs troupeaux, mais le reste du monde admire ce soleil.[end if][if nuit is happening]Il fait nuit noire, et les loups se réjouissent.[end if] Un réseau de grottes s[']étend vers le sud, à flan de montagne, tandis qu[']une forêt se trouve en contrebas, loin vers le nord. [if part1 is happening]Mais avant cela il faudra traverser le reste de la plaine qui reste complètement à découvert.[end if]". The printed name of plaine1 is "La plaine ensoleillée".

Plaine2 is a room. Plaine2 is north from plaine1. The description of Plaine2 is "[if crepuscule is happening]Vous ne pourrez atteindre l[']horizon ce soir, mais peut-être arriverez-vous à temps pour rejoindre les brebis stationnées avant cela.[end if][if nuit is happening]Dans la nuit cet endroit est lugubre. Et pas de lune pour réconforter le coeur, et l[']instinct de vos semblables.[otherwise] Les vertes plaines s[']étendent à flanc de côteau entre le nord et le sud, et remontent également sur les côtés est et ouest de la vallée, vers ses limites qui se trouvent plus loin, sous forme de rebords trop escarpés pour espérer pouvoir les rejoindre. Vous pouvez néanmoins aller voir à votre gauche et à votre droite des points d[']intérêt qui attirent votre attention.[end if]".  The printed name of plaine2 is "La plaine en contrebas.".

Instead of going down when in plaine1, try going north.
Instead of going down when in plaine2, try going north.

Grottes is a room. Grottes is south from plaine1. The description of grottes is "[if player is animalbeing and compagnon is humanbeing]Vous habitez là, triste et sans compagnie. Les murs décorés ne sont d[']aucun soulagement pour vous.[otherwise]Cet endroit vous rappelle que vous habitiez là fut un temps. Mais c[']est du passé maintenant.[end if] [if player is humanbeing and compagnon is humanbeing]Une lumière vous attire l[']oeil au sud.[end if]".

Some murs are scenery in grottes. The description of murs are "Les murs étaient couverts de peintures primitives décrivant des sacrifices d[']animaux domestiques, ainsi que des scènes avec des humains courant après d[']autres dans des rues stylisées.[if player is humanbeing] Maintenant que vous regardez un peu mieux, vous pouvez voir que les dessins plus hauts représentent une grande louve qui allaite des enfants humains, et un dieu-cerf qui nourrit et entraine des louveteaux.". Understand "murs decores" or "decorations" or "peintures" or "grotte" or "grottes" or "dessins" as murs.

The laniere is female in grottes. The printed name of laniere is "lanière en cuir". The description of laniere is "Une petite lanière, tressée avec quelques brins de cuirs bruns. Elle semble solide.". Understand "cuir" or "laniere de cuir" or "laniere" or "laniere en cuir" as laniere.

Instead of going south when in grottes:
	if the player is animalbeing
		begin;
		say "Continuer par là, sans lumière, sans guide, sans rien, c[']est la mort assurée : des gouffres s[']ouvrent jusqu[']au centre de la terre, et nul n[']en est jamais revenu, sinon dans une autre vie.";
		otherwise;
		continue the action;
		end if.

Instead of going east when in grottes, try going south. Instead of going west when in grottes, try going south.

Grottes2 is south from grottes. The printed name of grottes2 is "grottes".

Section 2 - Les domaines extérieurs 

Plaine3 is west from plaine2. Plaine4 is east from plaine2.
Plaine5 is north from plaine2.
Mer is west from plaine5.

Foret is east from plaine5. 

Section 3 - La plaine3

The description of plaine3 is "Cet endroit est un peu moins monotone que le reste de la plaine, car il est à la fois plus vallonné et il y a davantage de végétation qui se développe ici.". The printed name of plaine3 is "La vallée".


The cadole is female in plaine3. The description of cadole is "Une petite cabane en pierres plates, avec une entrée sur le côté, dos aux vents. Il y avait une ouverture dans son toit, pour laisser passer la fumée, mais en ce moment il n[']y avait aucun feu.". Understand "ouverture" or "cabane" or "petite cabane" or "pierres" or "pierre" or "pierres plates" or "entree" as cadole. 

Instead of entering cadole, say "Vous vous méfiez de ce genre de lieu clôt, même s[']il semble plus confortable que votre grotte. Et si c[']était un piège ou que cela s[']effondrait ?".

The chope de biere is female in plaine3. The description of chope de biere is "La chope reposait en équilibre sur une pierre.". The printed name of chope de biere is "chope de bière".

Instead of pushing chope de biere:
	say "La chope chavire, et se casse, répandant son liquide pour nourrir la terre.";
	move chope de biere to void
	
Instead of taking chope de biere:
	say "Vous êtes assez gauche pour faire ce genre de mouvement correctement.";
	try pushing chope de biere.
	
Every turn when in plaine3:
	if marcheur is in location and attaque is happening
		begin;
			if the player is carrying the crosse
				begin;
					say "L[']homme aperçoit le bâton appartenant [if player is masculin]à la bergère[otherwise]au berger[end if] et comprend aussitôt que quelque chose ne va pas. Il se saisit de la crosse, et court à votre suite en direction de la forêt et des loups.";
					move player to foret;
					move marcheur to foret;
					move crosse to marcheur;
					[move faune to foret;]
				end if;
			if the player is carrying the laniere, say "L[']homme regarde la lanière que vous avez dans la gueule, sans comprendre ce que vous voulez lui signifier.";
		end if;
	if marcheur is in location and vieux loup is in foret
		begin;
			if the player is carrying the crosse
				begin;
					say "L[']homme voit le bâton appartenant [if player is masculin]à la bergère[otherwise]au berge[end if] et comprend aussitôt que quelque chose ne va pas. Il prend la crosse, et court à votre suite en direction de la forêt et de ses dangers.";
					move player to foret;
					move marcheur to foret;
					move crosse to marcheur;
				end if;
			if the player is carrying the laniere, say "L[']homme regarde la lanière que vous avez dans la gueule, sans comprendre ce que vous voulez lui signifier.";
		end if;
	if marcheur is in plaine3 and marcheur is awake
		begin;
		say "L[']homme est là, assis sur une pierre à l[']entrée de la cadole.";
		if chope de biere is in location
			begin;
			say "[line break]Il prend une gorgée de bière, puis une seconde rasade à ce pichet, et avec encore de la mousse dans sa barbe il vous raconte une autre histoire, celle [if player is masculin]d[']une bergère[otherwise]d[']un berger[end if] d[']une grande beauté qui avait vécu dans la région, mais s[']était un jour égaré[if player is masculin]e[end if] dans la forêt, en ayant admiré un peu trop tard les roseaux et le coucher de soleil qui étaient si magnifiques par ici. Une meute de loup était arrivée, et l[']avait poursuivi[if player is masculin]e[end if] jusqu[']à ce qu['][if player is masculin]elle[otherwise]il[end if] tombe de fatigue puis et sous leurs crocs. Le grand dieu Faune en avait été fort affecté, lui qui n[']aimait guère que l[']on touche à ses enfants. Il avait tapé du sabot si fort que cela avait creusé une creuvasse connue maintenant sous le nom de 'la croix du bouc', que l[']on pouvait trouver plus loin vers le sud, dans le réseau de grottes, et où de nombreux promeneurs s[']étaient perdus.";
			move compagnon to plaine5;
			remove chope de biere from play;
			end if;
		if chope de biere is in void
			begin;
			say "L[']homme semble chercher quelque chose autour de lui. Ne trouvant rien, il s[']endort d[']un sommeil profond.";
			end the game saying "Vous regardez un moment l[']homme dans son sommeil, puis au bout de quelques heures, retournez vers votre grotte, un peu déçu[if player is feminin]e[end if].";
			end if;
	end if.

Section 4 - La plaine4

The description of plaine4 is "Comme le reste de cette région, cette plaine était recouverte d[']herbes douces et longues, pas encore mangées par les animaux en transhumance. Il n[']y avait pas un arbre ici, aucun endroit pour se mettre à couvert.". The printed name of plaine4 is "La plaine".

The description of plaine5 is "[if not visited]C[']est ici que commence la zone de pâturage pour les moutons. Ces animaux paissaient tranquillement jusqu[']à ce qu[']ils vous entendent. Alertés, ils se tiennent maintenant à distance, en surveillant les alentours. [end if][if nuit is happening]Vers les lueurs encore rémanentes du soleil couchant se trouve la mer, et dans l[']autre direction l[']entrée d[']une forêt. [otherwise]Les embruns de la mer donnent une odeur salée vers l[']ouest, tandis qu[']une forêt s[']ouvre vers l[']est.[end if]". The printed name of plaine5 is "Le fond de la vallée".

The description of mer is "La mer est calme, malgré le vent, mais quelques rides viennent nuancer sa surface. Le bord de mer est entouré de roseaux.". The printed name of mer is "Le bord de mer".

The ocean is female scenery in mer. Understand "mer" or "eau" or "rive" or "surface" as ocean. The description of ocean is "En se frayant un passage parmi les roseaux, vous vous approchez de la rive et regardez la mer. [if player is masculin and player is animalbeing]Un loup [otherwise if player is feminin and player is animalbeing]Une louve [otherwise if player is masculin and player is  humanbeing]Un bel homme [otherwise if player is feminin and player is humanbeing]Une belle femme [end if]se reflète là, avec une expression tout à la fois [if player is animalbeing]rogue[otherwise]mystérieuse[end if] et mélancolique. [if player is animalbeing]Quoi qu[']il en soit pas tout à fait le genre à plaire aux moutons.[end if]".

Some rides are female scenery in mer. 
Some roseaux are scenery in mer. Understand "roseau" as roseaux.

Instead of searching roseaux for the first time:
	say "Il y a un étendard détrempé dans les roseaux.";
	move étendard to location. 
	
The étendard is a thing. Understand "drapeau" or "etendard" or "banniere" as étendard. The description of étendard is "[if marcheur is in plaine3]Rien de plus à dire sur cet [']étendard.[otherwise]L[']étendard est enroulé, aussi vous ne pouvez rien en voir, si ce n'est qu'il est humide et semble vieux.[end if]".

Section 6 - La forêt 

The printed name of foret is "Forêt". The description of foret is "Une forêt sombre et ténébreuse, dont un chemin principal s[']ouvre vers la plaine à l[']ouest, et s[']enfonce vers le centre de la forêt vers l[']est.".

Every turn when in foret:
	if marcheur is in location and meute de loups is in location and meute de loups is aggressive
		begin;
		say "L[']homme lève les bras aux ciels, et tous les loups s[']accroupissent. Les yeux brillants, il leur parle doucement, puis de plus en plus vivement. La meute de loups se relève alors d[']un seul bond, et se disperse dans la forêt.";
		now meute de loups is docile;
		move meute de loups to void;
		move vieux loup to location;
		say "Seul un vieux loup reste là, l[']air insoumis et rebelle. L[']homme essaye de l[']approcher, arrive à l[']attraper une fois, mais le loup se débat de trop et il n[']est pas possible de faire quoi que ce soit avec.";
		end if;
	if marcheur is in location and vieux loup is in location and vieux loup is aggressive
		begin;
			say "[one of]Le vieux loup est toujours là, l[']air insoumis et rebelle. L[']homme essaye de l[']approcher, arrive à l[']attraper une fois, mais le loup se débat de trop et il n[']est pas possible de faire quoi que ce soit de bon avec.[or]L[']homme lutte toujours avec le vieux loup, sans arriver à le maîtriser.[stopping]";
			if the player is carrying the laniere or laniere is in location
				begin;
				say "Puis l[']homme agripper la lanière de cuir que vous lui tendez, forme un solide garrot, et attache le vieux loup avec, qui devient aussitôt docile.[paragraph break]";
				move laniere to marcheur;
				now vieux loup is docile;
				if faune is not in void
					begin;
					move faune to foret;
					say "Après avoir fini d[']attraper le loup, l[']homme se prépare à partir lorsqu[']une créature étrange, nue et avec des pieds de bouc, arrive.";
					otherwise;
					say "Après avoir fini d[']attraper le loup, l[']homme regarde la forme qui était il y a encore quelques instant [if player is feminin]un jeune humain. Un beau loup se trouve à sa place, [otherwise]une jeune humaine. Une belle louve se trouve à sa place, [end if]qui vous regarde avec la même expression que tout à l[']heure. L[']homme vous fait un clin d[']oeil, et repart avec le vieil animal dorénavant apaisé au bout de sa laisse, vers là où la forêt semble la plus dense, et après quelques pas vous ne les voyez bientôt plus.";
					now compagnon is animalbeing;
					move vieux loup to void;
					move marcheur to void;
				end if;
			end if;
		end if.

The sol is scenery in foret. The sol is an enterable supporter. 


Before going somewhere when the player is lying:
	say "Vous devez d[']abord vous relever.";
	stop the action. 

Before going nowhere when the player is lying:
	say "Vous devez d[']abord vous relever.";
	stop the action. 

Before exiting when the player is lying:
	now the player is standingup;
	say "Vous vous relevez.";
	move the player to location;
	stop the action.
	 
[il devait y avoir moyen de faire plus simple, mais je n[']avais pas compris que going nowhere était différent de going somewhere alors j[']avais toujours des messages d[']erreurs]

Before going somewhere during rencontre:
	say "Vous êtes tétanisé par l[']apparition du faune.";
	stop the action. 


Before going nowhere when the compagnon is in location and chasse is happening:
	say "Vous ne pouvez vous empêcher d[']arrêter votre contemplation.";
	stop the action. 

Before exiting when the compagnon is in location and chasse is happening:
	say "Vous ne pouvez vous empêcher d[']arrêter votre contemplation.";
	stop the action.


Instead of going east when the location is foret:
	if the compagnon is humanbeing
		begin;
		say "Vous n[']avez pas de raison de retourner seul[if player is feminin]e[end if] dans les profondeurs de cette forêt. Vous l'avez quittée jadis";
		otherwise;
		say "Vous faites un premier pas sur le chemin vers l[']est, et votre [if player is feminin]nouveau compagnon[otherwise]nouvelle compagne[end if] vous accompagne là aussi. Une nouvelle vie va pouvoir commencer pour vous deux.";
		end the game in victory;
		end if.

Foret2 is east from foret. The printed name of foret2 is "Forêt".

[
Every turn when the player is inside sol: bug TODO
	After reading a command:
		if the player's command includes "sud" or "nord" or "est" or "ouest" 
		begin;
			say "Vous devez d[']abord vous relever.";
			reject the player's command;
			end if.
			]


Void is a room.

Chapter 4 - Le décor 

Some herbes are female backdrop in Plaine1, in plaine2, in plaine3 and in Plaine4. Understand "herbe" as herbes.
Some ombres are female backdrop in Plaine1 and in Plaine2. Understand "ombre" as ombres.

The végétation is female backdrop in plaine3 and in plaine4. The description of végétation is "Des herbes sèches et hautes.". Understand "herbes seches" or "vegetation" as végétation.

Some arbres are backdrop in foret and in plaine4 and in plaine2 and in plaine1 and in plaine3 and in mer. 

The soleil is a backdrop. It is everywhere. 

Instead of listening when in Plaine1 or when in plaine2, say "Il n[']y avait que le son du vent au début. Puis, porté par cette brise, par vagues successives, filtraient progressivement les sons d[']une flûte en roseau.".

The flute is a backdrop. It is everywhere. Instead of listening to flute, say "Le son était indistinct, presque inaudible. Il vous faisait peur. Une peur panique !". The description of flute is "Elle n[']était pas visible, mais sa présence n[']augurait rien de bon pour vous et les vôtres.".

The vent is a backdrop. The vent is everywhere. Instead of listening to vent, try listening.

Some montagnes are a backdrop. Montagnes are in plaine1 and in plaine2. The description of montagnes are "Ces petites montagnes forment pour vous une barrière infranchissable vers le sud, et la seule manière de les atteindre est de passer par un chemin que vous n[']avez jamais osé prendre.". Understand "rebords" or "rebord" or "cotes" as montagnes.

Some PointsInteret are backdrop in plaine2. The description of PointsInteret is "Il faut vous approcher davantage pour voir ce qui se passe plus loin.". The printed name of PointsInteret is "points d[']intérêt". Understand "points" or "points d'interet" as PointsInteret.

Chapter 5 - Les animaux et les personnages

Section 1 - Les moutons

Some moutons are animals in plaine5. Understand "mouton" or "animaux" or "brebis" as moutons.

Instead of taking moutons:
	remove moutons from play;
	say "Les moutons se sauvent tous à votre approche.".
	
Instead of attacking or pushing or pulling moutons, try taking moutons.

Section 2 - Le marcheur

The marcheur is a man in plaine4. The description of marcheur is "Un homme avec des vêtements beiges et marrons, et une longue cape verte. Ses cheveux sont cachés par un large chapeau, et sa taille est très haute. Bien qu[']il n[']ait pas l[']air jeune, il semble sans âge.". The printed name of marcheur is "homme". Understand "chasseur" or "berger" or "vagabond" or "homme" or "cape" or "chapeau" or "cerf" or "barbe" or "cernunnos" as marcheur.

Instead of examining marcheur for the first time:
	say "Berger, chasseur ou simple vagabond ? Ennemi ou ami ?";
	now the printed name of marcheur is "vagabond".

Every turn when in plaine2:
	if marcheur is melancolique and étendard is in plaine4
		begin;
		move marcheur to plaine3;
		move étendard to marcheur;
		say "Vous voyez l[']homme vous suivre un moment et continuer vers l[']ouest.";
		end if.

Instead of jumping when marcheur is in location and attaque is happening:
		say "L[']homme vous regarde sans comprendre, et au bout d[']un moment votre énervement est communicatif, et il se doute que vous voulez lui signifier qu[']il y a un problème, aussi il vous suit jusqu[']à l[']orée de la forêt.";
		move player to foret;
		move marcheur to foret;
		move faune to foret.

Instead of jumping when marcheur is in location and vieux loup is in foret:
		say "L[']homme vous regarde sans comprendre, et au bout d[']un moment votre énervement est communicatif, et il se doute que vous voulez lui signifier qu[']il y a un problème, aussi il vous suit jusqu[']à l[']orée de la forêt.".
		move player to foret;
		move marcheur to foret.

Instead of dropping étendard when marcheur is in location, try giving étendard to marcheur.

The vetement is a thing. The vetement is wearable. The printed name of vetement is "vêtement". The description of vetement is "De simples habits.".
Understand "habits" or "simples habits" as vetement. 

Section 3 - Le compagnon

Compagnon is a person. The description of compagnon is "[if player is feminin and compagnon is humanbeing]Un jeune homme plein d[']enthousiasme[otherwise if player is masculin and compagnon is humanbeing]Une jeune femme riante et gaie[otherwise if player is feminin and compagnon is animalbeing]Un loup fougueux et énergique[otherwise if player is masculin and compagnon is animalbeing]une louve douce et puissante[end if]. [if compagnon is carrying crosse]Sa crosse à la main,[otherwise]Avec un sourire en coin,[end if] [if player is feminin]il[otherwise]elle[end if] vous regarde d[']un air étonné.". The printed name of compagnon is "[if player is feminin and compagnon is humanbeing]un berger[otherwise if player is masculin and compagnon is humanbeing]une bergère[otherwise if player is feminin and compagnon is animalbeing]un loup[otherwise if player is masculin and compagnon is animalbeing]une louve[end if]".
Understand "berger" or "bergere" or "homme" or "femme" or "fille" or "garcon" as compagnon.

The crosse is a female thing. Understand "baton" as crosse. The description of crosse is "Un long bâton recourbé à son extrémité, permettant d[']avoir un crochet utile en toutes circonstances.".

Instead of examining Compagnon when attaque is happening, say "[if player is feminin]Bien que de bonne constitution, il ne peut faire le poids face à ce danger, et est complètement effrayé par les événements.[otherwise]Malgré sa visible débrouillardise, elle n[']a pas d[']issue possible face à ce danger particulier, et malgré votre apparence elle semble attendre que vous fassiez quelque chose pour elle.[end if]".

Section 4 - La meute

The meute de loups is a female animal. Understand "loup" or "vieux loup" or "vieux" as meute de loups. 
The description of meute de loups is "[if meute de loups is aggressive]La meute comprend une dizaine de jeunes loups affamés et aggressifs, ainsi qu[']un plus vieux qui semble être le chef.[otherwise]Les loups semblent calmés à présent.[end if]".

Instead of giving étendard to marcheur:
	say "Vous déposez le drapeau aux pied de l[']homme. Il se penche, déplie la toile fanée et humide, et la déroule sur l[']herbe. Le rouge sanglant du fond, maintenant aussi éteint que la fleur de certains lilas, avait passé ses pâles couleurs au motif central de cet étendard : un soleil jaune et argent. [line break]L[']homme, avec de l[']émotion dans la voix, vous raconte alors l[']histoire de ce régiment qui s[']était égaré dans les marécages, et s[']était perdu corps et bien dans les sables mouvants, aussi étrange que cela puisse paraître.";
	now marcheur is melancolique;
	move étendard to location. 
	
Instead of attacking meute de loups:
	say "[if meute de loups is aggressive]Vous vous jetez courageusement sur les loups, vos frères. Malheureusement que peut l[']individu face à un groupe en furie ?[otherwise]Vous n[']avez pas de raison de les attaquer[end if]";
	end the game in death.
	
Instead of attacking meute de loups with crosse:
	say "[if meute de loups is aggressive]Vous courez vers les loups avec la crosse entre les dents, en fauchant certains au passage, semant la panique parmi les autres. Certains tombent sans connaissance, d[']autres se sauvent au fond des bois. À la fin seul un grand loup au regard vicieux vous fait face.[otherwise]Vous n[']avez pas de raison de les attaquer?[end if]";
	move vieux loup to location;
	move meute de loups to void;
	move faune to void.

Section 5 - Le vieux loup

The vieux loup is an animal. Understand "grand loup" or "loup" as vieux loup. The description of vieux loup is "Un vieux loup qui a toujours vécu de rapines et de fourberie. Vous en avez souvent entendu parler, sans jamais le rencontrer auparavant.".

Instead of attacking vieux loup when vieux loup is aggressive:
	say "Le loup grogne, gronde, vous fait reculer. Il est vraiment trop fort pour vous.".
	
Instead of attacking vieux loup with crosse when vieux loup is aggressive:
	say "Ça marchait pour les plus jeunes, mais un vieux filou comme lui ne se laisse pas prendre avec une arme aussi anodine."

Section 6 - Le Faune

The faune is a man. The description of faune is "Pourtant lui-même le dieu le plus seul et le plus mystérieux de tous, Pan est le faune protecteur des troupeaux et des animaux à l[']instinct grégaire. Mais il est également protecteur de leurs bergers, ces derniers parfois tout aussi seuls et mélancoliques dans leurs vallées. ".
Understand "pan" or "creature" as faune.


Chapter 6 - Rose des vents et goodies

Section 1 - Improved Status Line  (for Z-Machine only)

[from Emily Short's Bronze]

[This status line is designed to help the player track his exploration through the game: rooms that aren[']t explored yet are highlighted in red, and a count of explored rooms is kept in the lower left corner. We add the further refinement that, if the player is in a dark room, the compass line shows only exits which lead to lit rooms, on the assumption that he will be able to see lighted exits but not dark ones.]

Include Basic Screen Effects by Emily Short. [* An extension that provides phrases for changing text colors, modifying the status line more fully, pausing the game, and so on.]

To turn screen black:
	say white letters;
	turn the background black;
	clear the screen;
	leave space;
	
To turn screen white:
	turn the background white;
	say black letters;
	clear the screen;
	leave space.

To leave space:
	say paragraph break;
	say paragraph break;
	say paragraph break;
	say paragraph break. 
	
Table of Fancy Status
left	central	right 
""	" Lupercalia "	"[top rose]"
"  "	""	"[middle rose]"
" [if in darkness]Les ténèbres[otherwise][location][end if] "	""	"[bottom rose]"
 

To say red reverse:
	turn the background red.
	
To say black reverse:
	turn the background black.
	
To say white reverse:
	turn the background white.

Definition: a room is discernible: 
	if it is lit, yes;
	if in darkness, no;
	yes.

To say top rose:
	let place be the room up from the location;
	if the place is a discernible room, say "[if the place is unvisited][red reverse][end if]U   [default letters]"; otherwise say "    "; [* Originally I assumed that everyone would be using a white-letters-on-black status bar, and set the lettering to black and white after printing the first letter. This was a mistake, as many people on Windows Frotz have a default color scheme that makes the status bar blue-on-white -- so of course the white lettering on a white background didn[']t show at all. Moral: never ever make assumptions about the color scheme used by your audience.]
	let place be the room northwest from the location;
	if place is a discernible room, say "[if the place is unvisited][red reverse][end if]NW[default letters]"; otherwise say "  ";
	let place be the room north from the location;
	if place is a discernible room, say "[if the place is unvisited][red reverse][end if] N [default letters]"; otherwise say "   ";
	let place be the room northeast from the location;
	if place is a discernible room, say "[if the place is unvisited][red reverse][end if]NE[default letters]"; otherwise say "  ".
	
To say middle rose:
	say "    ";
	let place be the room west from the location;
	if place is a discernible room, say "[if the place is unvisited][red reverse][end if]W [default letters]"; otherwise say "  ";
	say " . ";
	let place be the room east from the location;
	if place is a discernible room, say "[if the place is unvisited][red reverse][end if] E[default letters]"; otherwise say "  ".
	
To say bottom rose:
	let place be the room down from the location;
	if the place is a discernible room, say "[if the place is unvisited][red reverse][end if]D   [default letters]"; otherwise say "    ";
	let place be the room southwest from the location;
	if place is a discernible room, say "[if the place is unvisited][red reverse][end if]SW[default letters]"; otherwise say "  ";
	let place be the room south from the location;
	if place is a discernible room, say "[if the place is unvisited][red reverse][end if] S [default letters]"; otherwise say "   ";
	let place be the room southeast from the location;
	if place is a discernible room, say "[if the place is unvisited][red reverse][end if]SE[default letters]"; otherwise say "  ".
	 
Rule for constructing the status line:
	fill status bar with Table of Fancy Status;
	say default letters;
	rule succeeds.  

Section 1 - Decoration (for Glulx only)

Include Simple Graphical Window by Emily Short.
	Figure of Decoration is the file "motif.png". 
	The graphics window pixel count is 100.
	The graphics window position is g-left.


Rule for starting the virtual machine:
	now the currently shown picture is the figure of Decoration;
	now the current graphics drawing rule is the tiled drawing rule.


When play begins:
	follow the current graphics drawing rule.


Section 3 - Boussole (for Glulx only)

Include Basic Screen Effects by Emily Short. [* An extension that provides phrases for changing text colors, modifying the status line more fully, pausing the game, and so on.]

To turn screen black:
	clear the screen;
	leave space;
	
To turn screen white:
	clear the screen;
	leave space.

To leave space:
	say paragraph break;
	say paragraph break;
	say paragraph break;
	say paragraph break. 
	
Table of Fancy Status
left	central	right 
""	" Lupercalia "	"[top rose]"
"  "	""	"[middle rose]"
" [if in darkness]Les ténèbres[otherwise][location][end if]"	""	"[bottom rose]"
 

Definition: a room is discernible: 
	if it is lit, yes;
	if in darkness, no;
	yes.

To say top rose:
	let place be the room up from the location;
	if the place is a discernible room, say "[if the place is unvisited][end if]U   "; otherwise say "    "; [* Originally I assumed that everyone would be using a white-letters-on-black status bar, and set the lettering to black and white after printing the first letter. This was a mistake, as many people on Windows Frotz have a default color scheme that makes the status bar blue-on-white -- so of course the white lettering on a white background didn[']t show at all. Moral: never ever make assumptions about the color scheme used by your audience.]
	let place be the room northwest from the location;
	if place is a discernible room, say "[if the place is unvisited][end if]NW"; otherwise say "  ";
	let place be the room north from the location;
	if place is a discernible room, say "[if the place is unvisited][end if] N "; otherwise say "   ";
	let place be the room northeast from the location;
	if place is a discernible room, say "[if the place is unvisited][end if]NE"; otherwise say "  ".
	
To say middle rose:
	say "    ";
	let place be the room west from the location;
	if place is a discernible room, say "[if the place is unvisited][end if]W "; otherwise say "  ";
	say " . ";
	let place be the room east from the location;
	if place is a discernible room, say "[if the place is unvisited][end if] E"; otherwise say "  ".
	
To say bottom rose:
	let place be the room down from the location;
	if the place is a discernible room, say "[if the place is unvisited][end if]D   "; otherwise say "    ";
	let place be the room southwest from the location;
	if place is a discernible room, say "[if the place is unvisited][end if]SW"; otherwise say "  ";
	let place be the room south from the location;
	if place is a discernible room, say "[if the place is unvisited][end if] S "; otherwise say "   ";
	let place be the room southeast from the location;
	if place is a discernible room, say "[if the place is unvisited][end if]SE"; otherwise say "  ".
	 
Rule for constructing the status line:
	fill status bar with Table of Fancy Status;
	rule succeeds. 

Section 4 - Test me

test me with "test 1/test 2"

test 1 with "sud/prendre laniere/nord/nord/laisser laniere/nord/ouest/regarder la mer/fouiller les roseaux/prendre le drapeau/est/sud/est/x homme/donner etendard a homme/ouest/ouest/est/nord".

test 2 with "attendre/attendre/prendre la crosse/sud/ouest/ouest/sud/prendre la laniere/nord/est/examiner la creature/attendre/attendre/attendre/attendre/se lever/prendre le vetement/mettre le vetement/ouest/sud/sud/sud/regarder les dessins/sud".

test 3 with "attendre/attendre/prendre la crosse/est/attaquer les loups avec la crosse/attaquer le vieux loup/ouest/sud/ouest/ouest/sud/prendre la laniere de cuir/nord/est/est".


